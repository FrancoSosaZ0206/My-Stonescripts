// *******************************************************
//                      8-Temple.txt                      
// *******************************************************

// Script for Temple



// IMPORTS

var u = new MyScripts/Lib/Utilities
var uimkr = new MyScripts/Lib/UI_Maker
var p
p = import MyScripts/Lib/Performance
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat

/***************************************************************/

var som = new MyScripts/Lib/Speed_O_Meter
var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED
var bosTmr
bosTmr = import MyScripts/Borrowed/BossfightTimer2

var somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)


dpsm.ENABLED = true
dpsm.refreshRate = 30

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***************************************************************/



// VARIABLES

var smiteScr = 2
var keepMask = true
var keepFTali = false

?p.f = "boss"
  keepFTali = true

var canDebI
var canDebP
var canDebF

?p.ll
  keepMask = false
  keepFTali = false


// FUNCTIONS:

func ldtF(mode) // loadouts

  ?mode = "default"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield2@
  :?mode = "engage"
    equipL triskelion
    ?!keepMask
      equipR @a.ishield@
  :?mode = "escape"
    equipL mind
    ?!keepMask
      equipR @a.ishield@
  :?mode = "magnet"
    equipL star
    ?!keepMask
      equipR triskelion

  :?mode = "meleeDmg"
    equipL Bifrost
    ?!keepMask
      equipR @a.isword@
  :?mode = "meleeDef"
    equipL Bifrost
    ?!keepMask
      equipR @a.ishield@

  :?mode = "extend"
    equipL triskelion
    equipR mask

  :?mode = "magic"
    ?keepMask
      ?canDebI
        equipL @a.dIwand1@
      :?canDebP
        equipL @a.dPwand@
      :?canDebF
        equipL @a.dFwand@
    :
      //Left hand:
      ?canDebP
        equipL @a.dPwand@
      :?canDebF
        equipL @a.dPwand@
      :?canDebI
        equipL @a.dIwand2@
      
      //Right hand:
      ?canDebI
        equipR @a.dIwand1@

  :
    >o0,0,#red,ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight1v1()
  p.fds = foe.debuffs.string
  p.bs = buffs.string

  /* How to apply debuffs:
  - apply all except the ones the foe is immune to.
  Finish when:
  - all possible debuffs have been applied. */
  canDebI = (p.fds ! "chill:6" & p.f ! "immune_to_debuff_chill")
  canDebP = (p.fds ! "damage" & p.f ! "immune_to_debuff_damage")
  canDebF = (p.fds ! "dot" & p.f ! "immune_to_debuff_dot")
  
  ?(keepMask | p.bs = "berserk") & (canDebI | canDebP | canDebF)
    ?keepMask
      ?canDebI
        equipL @a.dIsword@
      :?canDebP
        equipL @a.dPsword1@
      :?canDebF
        equipL @a.dFsword@
    :
      //Left hand:
      ?canDebP
        equipL @a.dPsword1@
      :?canDebF
        equipL @a.dFsword@
      :?canDebI
        equipL @a.dIwand1@

      //Right hand:
      ?canDebI
        equipR @a.dIsword@

  :?keepMask
    equipL Bifrost
  :
    p.bs = buffs.string
    ?p.bs = "berserk"
      c.doMoondial(Bifrost, a.isword, 2)
    :
      c.doMoondial(Bifrost, a.isword, 0)

  return



// PROCEDURE:

/* Left hand:
--- do AAC ---
- when state = 3
--- do NOT do AAC ---
- When ask is on and equipped weapon is a +21 sword. */
?p.ils >= 3 & !(keepMask & p.il = "sword D +21")
  equipL sword 0*
  equipL @p.il@
?p.irs >= 3 & p.ir ! "sword D +21"
  equipR @a.cshield1@
  equipR @p.ir@

?hp < maxhp & !keepMask
  loc.Leave()

?p.lb // AUTO BREWING
  p.rs = res.stone
  p.rw = res.wood
  p.rb = res.bronze

  ?p.rb >= 10
    ?p.rs >= 10
      ?p.ip ! "lucky"
        brew stone + bronze

    :?p.rw >= 10
      ?p.ip ! "berserk"
        brew wood + bronze
    :
      loc.Leave()
  :
    loc.Leave()

?keepMask
  equipR mask

?p.lb | p.ll
  equipL triskelion
  equipR @a.cshield1@

:?c.canUseAETali() & !keepFTali
  ?keepMask
    c.useAETali("l")
  :
    equipL triskelion
    c.useAETali("r")

:?c.canUseFTali() & keepFTali
  ?keepMask
    c.useFTali("l")
  :
    equipL triskelion
    c.useFTali("r")

:?c.canUsePotDmg() & keepMask
  activate potion

:?pickup.distance ! 9999
  ?c.canUseQstaff()
    c.useQstaff(a.myqstaff)
  :
    ldtF("magnet")
:
  p.fd = foe.distance
  p.aiE = ai.enabled

  ?c.canUseBladeDS(smiteScr)
    ?p.fd <= 33
      c.useBladeDS()

  ?c.canUseQstaff() & p.aiE & !keepMask
    ?p.ir ! "bardiche" & p.ir ! Kubikiribocho
    ^& p.ir ! "heavy hammer" & (
    ^(!c.canBash() & !c.canDash())
    ^| (p.fd < 11 | p.fd > 15))
      c.useQstaff(a.myqstaff)

  :?p.fd > 35
    p.pa = armor
    ?14 <= p.pa & p.pa <= 29
      ldtF("default")
    :?p.pa < 14
      ldtF("armorRegen")
    :
      equipL triskelion
      equipR mask

  :?p.fd > 15
    ldtF("engage")

    var isAbtToDash
    isAbtToDash = (p.fd <= 17)
    ?keepMask & isAbtToDash
      equipR @a.ishield@

    p.fds = foe.debuffs.string
    p.bs = buffs.string
    canDebI = (p.fds ! "chill" &
    ^f ! "immune_to_debuff_chill")

    ?!keepMask & canDebI & isAbtToDash &
    ^i.getFoeTotalHP() > i.getBuffInfo("smite", false, 2) * a.smiteMult
      equipR @a.aiShield@

  :
    ?p.f ! "nagaraja"
      ?c.canBash()
        c.bash(Bifrost)
      :?c.canDash()
        c.dash(Bifrost, a.dshield)
      :
        ?p.f = "acronian_cultist"
          ?c.canUseMask()
            c.useMask()
          :
            fight1v1()  
        :
          p.fc = foe.count

          ?p.fc >= 7 &
          ^c.canUseHamm() &
          ^!c.canUseBlade()
            c.useHamm()

          :?p.fc > 1
            ldtF("magic")
          :?p.fd < 7
            fight1v1()


    : // foe = "nagaraja"
      p.fds = foe.debuffs.string
      p.fs = foe.state
      p.ft = foe.time

      ?c.canUseCind() & dpsm.DPS ! "N/A"
        ?p.fds = "ignition:15" |
        ^c.canKillCind(i.getFoeTotalHP(),
        ^              i.getDebuffLvl("ignition",true),
        ^              a.cindDmg, dpsm.DPS)
          c.useCind()

      ?c.canUsePot()
        activate potion

      ?(p.fs = 112 | p.fs = 102)
      ^& p.ft >= 55 & p.ft <= 65
        ?c.canUseMind()
          ldtF("escape")
        :?p.fd < 11
          ldtF("meleeDef")
        :
          ?keepMask
            equipL @a.altbow@
          :
            equip @a.ibow@

      :?p.fs ! 4 // if it's not dying
        ?c.canBash()
          c.bash(Bifrost)
        :?c.canDash()
          c.dash(Bifrost,a.dshield)

        :?c.canUseBard() & keepMask
          c.useBard(Kubikiribocho)
        :
          fight1v1()



// *******************************************************
//                  END OF "8-Temple.txt"                 
// *******************************************************