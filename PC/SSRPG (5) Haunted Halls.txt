// *******************************************************
//                       5-Halls.txt                      
// *******************************************************


//Script for Haunted Halls


//IMPORTS


var u = import MyScripts/Imports/utilities
var p
p = import MyScripts/Imports/performanceV2
var i
i = import MyScripts/Imports/MyBetterInfo
var c = import MyScripts/Imports/combat
var a = import MyScripts/Imports/MyArsenal


var som
som = import MyScripts/Imports/Speed_O_Meter






// VARIABLES




var pausedOnce
var smiteScr = 1
var runCount = 0
var keepMask = true


?p.lb | p.ll
  pausedOnce = false


?p.ll
  keepMask = false
  ?p.tt = 1
    runCount++


    ?p.ls > 5 & smiteScr ! 4
      smiteScr++




// FUNCTIONS:


func Fight1v1()
  p.fds = foe.debuffs.string
  p.php = hp
  p.pmhp = maxhp


  ?keepMask
    ?p.fds ! "chill:6"
      equipL @a.dIsword@
    :?p.fds ! "damage"
      equipL @a.dPsword1@
    :?p.fds ! "dot"
      equipL @a.dFsword@
    :?p.php ! p.pmhp
      equipL @a.dLsword@
    :
      equipL Dothaneel


  :
    //Left hand:
    ?p.fds ! "chill:6"
      equipL @a.dIsword@
    :?p.php ! p.pmhp
      equipL @a.dLsword@
    :
      equipL @a.vsword@


    //Right hand:
    ?p.fds ! "damage"
      equipR @a.dPsword1@
    :?p.fds ! "dot"
      equipR @a.dFsword@
    :
      equipR Dothaneel
  
  return




// LOADOUTS:


func ldtF(mode)


  ?mode = "default"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield1@
  :?mode = "armorRecover"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield2@
  :?mode = "engage"
    equipL triskelion
    ?!keepMask
      equipR @a.vshield@
  :?mode = "escape"
    equipL mind
    ?!keepMask
      equipR @a.vshield@
  :?mode = "magnet"
    equipL star
    ?!keepMask
      equipR triskelion




  :?mode = "meleeDmg"
    ?keepMask
      equipL Dothaneel
    :
      equipL @a.vsword@
      equipR Dothaneel


  :?mode = "magic"
    ?keepMask
      ?p.fds ! "chill:6" &
      ^p.f ! "immune_to_debuff_chill"
        equipL @a.dIwand1@
      :?p.fds ! "damage" &
      ^p.f ! "immune_to_debuff_damage"
        equipL @a.dPwand@
      :?p.fds ! "dot" &
      ^p.f ! "immune_to_debuff_dot"
        equipL @a.dFwand@
      :
        equipL @a.vwand1@


    :
      //Left hand:
      ?p.fds ! "damage" &
      ^p.f ! "immune_to_debuff_damage"
        equipL @a.dPwand@
      :?p.fds ! "dot" &
      ^p.f ! "immune_to_debuff_dot"
        equipL @a.dFwand@
      :
        equipL @a.vwand2@


      //Right hand:
      ?p.fds ! "chill:6" &
      ^p.f ! "immune_to_debuff_chill"
        equipL @a.dIwand1@
      :
        equipR @a.vwand1@




  :
    >c-10,0,#red,
    ^ERROR: ldtF() recieved incorrect mode: @mode@
  return




// PROCEDURE:


var somui = som.showSpeedOmeterUI(1,6,i.colorToHex("pink"),false)
?som.speedOmeterUpdate(false)
  som.showSpeedOmeterUI(1,6,i.colorToHex("pink"),false)
?somui
  somui.Recycle()
  somui = null




?p.ils = 3
  equipL wand 0*
  equipL @p.il@
?p.irs = 3
  equipR @a.cshield1@
  equipR @p.ir@


?p.lb
  p.rs = res.stone
  p.rw = res.wood
  p.rb = res.bronze


  ?p.rb >= 10
    ?p.rw >= 10
      ?p.ip ! "berserk"
        brew wood + bronze
    :?p.rs >= 10
      ?p.ip ! "lucky"
        brew stone + bronze
    :
      loc.Leave()
  :
    loc.Leave()


?keepMask
  equipR mask


?p.lb | p.ll
  equipL triskelion
  equipR @a.cshield1@


:?c.canUseTali()
  equipL triskelion
  c.useTali("r")


:?c.canUsePot() & keepMask
  activate potion


:?pickup.distance ! 9999
  ldtF("magnet")


:
  p.fd = foe.distance
  p.aiE = ai.enabled


  ?c.canUseQstaff()
    ?p.aiE &
    ^p.ir ! "heavy hammer" &
    ^p.ir ! "bardiche" &
    ^p.ir ! Kubikiribocho & (
    ^(!c.canBash() & !c.canDash()) |
    ^(p.fd < 11 | p.fd > 15))
      c.useQstaff(a.myqstaff)


  ?c.canUseBladeDS(smiteScr)
    ?p.fd <= 33
      c.useBladeDS()


  ?p.fd > 35
    p.pa = armor


    ?p.pa > 29 & p.php ! p.pmhp
      ?!keepMask
        equipL triskelion
        equipR ouroboros
      :
        equipL ouroboros


    :?14 <= p.pa & p.pa <= 29
      ldtF("default")
    :?p.pa < 14
      ldtF("armorRecover")


    :
      equipL triskelion
      equipR mask


  :?p.fd > 15
    p.fs = foe.state


    ldtF("engage")
    ?p.php ! p.pmhp & p.f = "pallas" & p.fs = 0
      ?!keepMask
        equipR ouroboros
      :
        equipL ouroboros


  :?c.canBash()
    c.bash(a.dLsword)
  :?c.canDash()
    c.dash(a.dLsword,a.dshield)


  :
    p.fc = foe.count




    ?c.canUseHamm() & !c.canUseBlade() &
    ^((smiteScr < 4 & p.si = 5) |
    ^ (smiteScr = 4 & p.si = 3))
      c.useHamm()


    :?p.f = "spawner" & p.fc = 1
      Fight1v1()


    :?p.f ! "pallas"
      ldtF("magic")


    : // p.f = "pallas"
      p.fs = foe.state


      ?p.fs = 100 & !pausedOnce
        // 100 = entering phase2
        pausedOnce = true
        loc.Pause()


      :?p.fs ! 4
        ?c.canUsePot()
          activate potion
        :?c.canUseMask()
          c.useMask()
        :
          Fight1v1()


// *******************************************************
//                  END OF "5-Halls.txt"                  
// *******************************************************