// *******************************************************
//                       6-Mine.txt                       
// *******************************************************

// Script for Boiling Mine



// IMPORTS

var u = new MyScripts/Lib/Utilities
var uimkr = new MyScripts/Lib/UI_Maker
var p
p = import MyScripts/Lib/Performance
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat
c.Main()

/***************************************************************/

var som = new MyScripts/Lib/Speed_O_Meter
var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED
var bosTmr
bosTmr = import MyScripts/Borrowed/BossfightTimer2

var somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)


dpsm.ENABLED = true
dpsm.refreshRate = 30

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***************************************************************/



// VARIABLES

var runCount = 0
var keepMask = true

var smiteScr = 1
var weavScr = 4

var canDebI
var canDebP
var canDebF

var keepFTali = false

?p.f = "boss"
  keepFTali = true

var rangedDebCount = 0

?p.ll
  keepMask = false
  keepFTali = false
  rangedDebCount = 0

  ?p.tt = 1
    runCount++

    ?p.ls > 5 & runCount >= 2 & smiteScr ! 3
      smiteScr++



// FUNCTIONS

func ldtF(mode) // loadouts

  ?mode = "default"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield1@
  :?mode = "armorRegen"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield2@
  :?mode = "engage"
    equipL triskelion
    ?!keepMask
      equipR @a.aeshield@
  :?mode = "escape"
    equipL mind
    ?!keepMask
      equipR @a.aeshield@
  :?mode = "magnet"
    equipL star
    ?!keepMask
      equipR triskelion

  :?mode = "extend"
    equipL triskelion
    equipR mask

  :?mode = "unmkDmg"
    ?p.tt % 2 = 0 & p.fd > 4
      equipL triskelion
      equipR hatchet
    :
      equip @a.dUstaff@

  :?mode = "rangedDmg"
    equip @a.aebow@
  :?mode = "rangedP"
    equip @a.dPbow@
  :?mode = "rangedF"
    equip @a.fbow@

  :?mode = "magic"
    ?keepMask
      ?canDebI
        equipL @a.dIwand1@
      :?canDebP
        equipL @a.dPwand@
      :?canDebF
        equipL @a.dFwand@

    :
      ?p.tt % 4 = 0 & p.fd > 4
        equipL triskelion
        equipR hatchet

      :?canDebI
        equip @a.dIstaff@
      :?canDebP
        equip @a.dPstaff@
      :?canDebF
        equip @a.dFstaff@

  :
    >`0,2,#red,ERROR: ldtF() recieved incorrect mode: @mode@

  return

func fight1v1()
  p.fds = foe.debuffs.string
  p.php = hp
  p.pmhp = maxhp

  /* How to apply debuffs:
  - apply all except the ones the foe is immune to.
  Finish when:
  - all possible debuffs have been applied. */
  canDebI = (p.fds ! "chill:6" & p.f ! "immune_to_debuff_chill")
  canDebP = (p.fds ! "damage" & p.f ! "immune_to_debuff_damage")
  canDebF = (p.fds ! "dot" & p.f ! "immune_to_debuff_dot")

  ?canDebI | canDebP | canDebF
    ?keepMask
      ?canDebI
        equipL @a.dIsword@
      :?canDebP
        equipL @a.dPsword@
      :?canDebF
        equipL @a.dFsword@
    :
      //Left hand:
      ?canDebP
        equipL @a.dPsword@
      :?canDebF
        equipL @a.dFsword@
      :?canDebI
        equipL @a.dIwand1@

      //Right hand:
      ?canDebI
        equipR @a.dIsword@

  :?p.php ! p.pmhp
    ?keepMask
      equipL @a.dLsword@
    :
      equipL moon
      equipR @a.dLsword@

  :
    ?keepMask
      equipL Void Slayer
    :
      p.bs = buffs.string
      ?p.bs = "berserk"
        c.doMoondial(Void Slayer, a.aesword, 2)
      :
        c.doMoondial(Void Slayer, a.aesword, 0)

  return



// PROCEDURE:


c.doAAC("sword 0*", a.cshield1)

?p.lb // AUTO BREWING:
  p.rs = res.stone

  ?p.rs >= 20
    ?p.ip ! "strength"
      brew stone
  :
    loc.Leave()

?keepMask
  ?p.ir ! "bardiche" &
  ^p.ir ! "quarterstaff" &
  ^p.ir ! "heavy hammer"
    equipR mask

?p.lb | p.ll
  equipL triskelion
  equipR @a.cshield1@

:?c.canUseAETali() & !keepFTali
  ?keepMask
    c.useAETali("l")
  :
    equipL triskelion
    c.useAETali("r")

:?c.canUseFTali() & keepFTali
  ?keepMask
    c.useFTali("l")
  :
    equipL triskelion
    c.useFTali("r")

:?c.canUsePot() & keepMask
  activate potion

:?pickup.distance < 15
  ldtF("magnet")
:
  p.fd = foe.distance
  p.aiE = ai.enabled

  ?c.canUseBladeDS(smiteScr)
    c.useBladeDS()

  /****************************/
  ?c.canUseWeav()
    ?p.si = weavScr & p.fd < 15
    ^& keepMask
      c.useWeav()
  // **************************/

  ?p.fd > 35
    p.pa = armor

    ?c.canUseQstaff() & p.aiE
    ^& p.ir ! "bardiche" & p.ir ! Kubikiribocho
    ^& p.ir ! "heavy hammer"
      c.useQstaff(a.myqstaff)

    :?14 <= p.pa & p.pa <= 29
      ldtF("default")
    :?p.pa < 14
      ldtF("armorRegen")

    :
      equipL triskelion
      equipR mask
  :
    p.fds = foe.debuffs.string

    ?p.f ! "bronze_guardian"
      ?p.fd > 15
        ?c.canUseQstaff() & p.aiE
        ^& p.ir ! "bardiche" &
        ^  p.ir ! Kubikiribocho &
        ^  p.ir ! "heavy hammer" &
        ^(p.f ! "explode" & p.fd > 17)
          c.useQstaff(a.myqstaff)

        :
          ldtF("engage")

          var isAbtToDash
          isAbtToDash = (p.fd <= 17)
          ?keepMask & isAbtToDash
            equipR @a.aeshield@

          canDebI = (p.fds ! "chill" &
          ^f ! "immune_to_debuff_chill")
          p.bs = buffs.string

          ?!keepMask & canDebI & isAbtToDash &
          ^i.getFoeTotalHP() > i.getBuffInfo("smite", false, 2) * a.smiteMult
            equipR @a.aiShield@

      :?c.canBash()
        c.bash(Void Slayer)
      :?c.canDash()
        c.dash(Void Slayer,a.dshield)

      :?p.f = "explode" // minecart miniboss
        ?armor < 80 // we don't need to escape
          ldtF("escape") // if we can tank the damage.

      :?c.canUseQstaff() & p.aiE
      ^& p.ir ! "bardiche"
      ^& p.ir ! Kubikiribocho
      ^& p.ir ! "heavy hammer"
        c.useQstaff(a.myqstaff)

      :
        ?c.canUseHamm() & keepMask
        ^& p.fd < 15 &
        ^((smiteScr = 1 & p.si = 3) |
        ^ smiteScr = 3 & p.si = 2    )
          c.useHamm(a.myHamm)

        :?p.fc = 1
          fight1v1()

        :
          canDebI = (p.fds ! "chill:6" & p.f ! "immune_to_debuff_chill")
          canDebP = (p.fds ! "damage" & p.f ! "immune_to_debuff_damage")
          canDebF = (p.fds ! "dot" & p.f ! "immune_to_debuff_dot")

          ?canDebI | canDebP | canDebF
            ldtF("magic")
          :
            ldtF("unmkDmg")


    : // p.f = "bronze_guardian"
      p.fs = foe.state
      p.ft = foe.time

      ?p.fs = 4 // dying
        ldtF("magnet")

      :?p.fs ! 1 // boss appearing cinematic
        ?c.canUseCind() & keepMask & dpsm.DPS ! "N/A"
          var currIgnLvl = 0
          currIgnLvl = i.getDebuffLvl("ignition", true)

          ?p.fds = a.maxIgnitions_Default |
          ^(currIgnLvl > 3 &
          ^c.canKillCind(i.getFoeTotalHP(),
          ^              currIgnLvl,
          ^              a.cindDmg_Default, dpsm.DPS))
            c.useCind()

        ?c.canUsePotDmg() |
        ^c.canUsePotSpecial("strength",0)
          activate potion
        :?c.canUseHamm() // & keepMask
          c.useHamm(a.myHamm)
        :?c.canUseMask() // & keepMask
          c.useMask()

        :?p.fs = 32 & p.ft >= u.sec // boss's hammer attack
          ldtF("escape")

        :?p.fs = 33 & p.ft < 6 * u.sec // boss's hammer's stuck

          ?p.ft > 5 & c.canUseQstaff()
            c.useQstaff(a.myqstaff)

          :?p.fd > 15
            equipL triskelion // get close fast to boss
            ?!keepMask
              equipR Void Slayer
          :?c.canBash()
            c.bash(Void Slayer)
          :?c.canDash()
            dash(VoidSlayer,a.dshield)

          :
            ?c.canUseBard() & keepMask
              c.useBard(Kubikiribocho)
            :
              fight1v1()

        :
          ?keepMask
            ?p.ils = 3
              rangedDebCount++

            ?rangedDebCount = 0
              equipL @a.dFwand@
            :?rangedDebCount = 1
              equipL @a.dPwand@
            :
              equipL @a.altbow@

          :
            ?p.irs = 3
              rangedDebCount++

            ?rangedDebCount = 0
              equip @a.dFstaff@
            :?rangedDebCount = 1
              equip @a.dPstaff@
            :
              ldtF("rangedDmg")

          /* :
            ?p.fa < 1
              ?c.canUseMask() & !keepMask
                c.useMask()
              :?c.canUseHamm() & !keepMask & p.ip = "strength"
                c.useHamm(a.myHamm)
              :
                var pBowCount = rangedDebCount + 1
                ?p.fds ! "damage" & pBowCount ! rangedDebCount
                ^& p.ip = "strength"
                  ldtF("rangedP")

                :?keepMask
                  equipL @a.altbow@
                :
                  ldtF("rangedDmg")
          */


// *******************************************************
//                   END OF "6-Mine.txt"                  
// *******************************************************