// *******************************************************
//                       5-Halls.txt                      
// *******************************************************

// Script for Haunted Halls



// IMPORTS

var u = new MyScripts/Lib/Utilities
var uimkr = new MyScripts/Lib/UI_Maker
var p
p = import MyScripts/Lib/Performance
var a = new MyScripts/Lib/MyArsenal
var i = new MyScripts/Lib/MyBetterInfo
var c = new MyScripts/Lib/Combat
c.Main()

/***************************************************************/

var som = new MyScripts/Lib/Speed_O_Meter
var dpsm = new MyScripts/Lib/DPS_Meter_MODIFIED
var bosTmr
bosTmr = import MyScripts/Borrowed/BossfightTimer2

var somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)
?som.Update(false)
  somPanel.Recycle()
  somPanel = null

  somPanel = som.mkUI(0, 1, i.colToHex("red"), true, false)


dpsm.ENABLED = true
dpsm.refreshRate = 30

var dpsPanel
?dpsm.Update()
  ?dpsPanel
    dpsPanel.Recycle()
    dpsPanel = null

  dpsPanel = dpsm.mkUI(0, 3, "#yellow", 2)

/***************************************************************/



// VARIABLES

var pausedOnce
var runCount = 0
var keepMask = true

var canDebI
var canDebP
var canDebF

var smiteScr = 1
var weavScr = 4

?smiteScr = 3
  weavScr = 2

var keepFTali = false

?p.f = "boss"
  keepFTali = true

?p.lb | p.ll
  pausedOnce = false

?p.ll
  keepMask = false
  keepFTali = false
  ?p.tt = 1
    runCount++

    ?p.ls > 5 & smiteScr ! 4
      smiteScr++



// FUNCTIONS:

func fight1v1()
  p.fds = foe.debuffs.string
  p.php = hp
  p.pmhp = maxhp


  /* How to apply debuffs:
  - apply all except the ones the foe is immune to.
  Finish when:
  - all possible debuffs have been applied. */
  canDebI = (p.fds ! "chill:6" & p.f ! "immune_to_debuff_chill")
  canDebP = (p.fds ! "damage" & p.f ! "immune_to_debuff_damage")
  canDebF = (p.fds ! "dot" & p.f ! "immune_to_debuff_dot")
  
  ?canDebI | canDebP | canDebF
    ?keepMask
      ?canDebI
        equipL @a.dIsword@
      :?canDebP
        equipL @a.dPsword@
      :?canDebF
        equipL @a.dFsword@
    :
      //Left hand:
      ?canDebP
        equipL @a.dPsword@
      :?canDebF
        equipL @a.dFsword@
      :?canDebI
        equipL @a.dIwand1@

      //Right hand:
      ?canDebI
        equipR @a.dIsword@

  :?p.php ! p.pmhp
    ?keepMask
      equipL @a.dLsword@
    :
      equipL moon
      equipR @a.dLsword@

  :
    ?keepMask
      equipL Dothaneel
    :
      p.bs = buffs.string
      ?p.bs = "berserk"
        c.doMoondial(Dothaneel, a.vsword, 2)
      :
        c.doMoondial(Dothaneel, a.vsword, 0)

  return

func ldtF(mode) // loadouts

  ?mode = "default"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield1@
  :?mode = "armorRecover"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield2@
  :?mode = "engage"
    equipL triskelion
    ?!keepMask
      equipR @a.vshield@
  :?mode = "escape"
    equipL mind
    ?!keepMask
      equipR @a.vshield@
  :?mode = "magnet"
    equipL star
    ?!keepMask
      equipR triskelion

  :?mode = "extend"
    equipL triskelion
    equipR mask

  :?mode = "meleeDmg"
    ?keepMask
      equipL Dothaneel
    :
      equipL @a.vsword@
      equipR Dothaneel
  :?mode = "wandDmg"
    ?keepMask
      equipL theVwand
    :
      equipL @a.vwand@
      equipR theVwand

  :?mode = "magic"
    ?keepMask
      ?canDebI
        equipL @a.dIwand1@
      :?canDebP
        equipL @a.dPwand@
      :?canDebF
        equipL @a.dFwand@

    :
      ?p.tt % 2 = 0
        equipL triskelion
        equipR hatchet

      :?canDebI
        equip @a.dIstaff@
      :?canDebP
        equip @a.dPstaff@
      :?canDebF
        equip @a.dFstaff@

  :
    >c-10,0,#red,
    ^ERROR: ldtF() recieved incorrect mode: @mode@

  return



// PROCEDURE:

c.doAAC("sword 0*", a.cshield1)

?p.lb
  p.rs = res.stone
  p.rw = res.wood
  p.rb = res.bronze

  ?p.rb >= 10
    ?p.rw >= 10
      ?p.ip ! "berserk"
        brew wood + bronze
    :?p.rs >= 10
      ?p.ip ! "lucky"
        brew stone + bronze
    :
      loc.Leave()
  :
    loc.Leave()

?keepMask
  ?p.ir ! "bardiche" &
  ^p.ir ! "quarterstaff" &
  ^p.ir ! "heavy hammer"
    equipR mask

?p.lb | p.ll
  equipL triskelion
  equipR @a.cshield1@

:?c.canUseAETali() & !keepFTali
  ?keepMask
    c.useAETali("l")
  :
    equipL triskelion
    c.useAETali("r")

:?c.canUseFTali() & keepFTali
  ?keepMask
    c.useFTali("l")
  :
    equipL triskelion
    c.useFTali("r")

:?c.canUsePot() & keepMask
  activate potion

:?pickup.distance ! 9999
  ldtF("magnet")
:
  p.fd = foe.distance

  ?c.canUseQstaff()
    ?ai.enabled &
    ^p.ir ! "heavy hammer" &
    ^p.ir ! "bardiche" &
    ^p.ir ! Kubikiribocho & (
    ^(!c.canBash() & !c.canDash()) |
    ^(p.fd < 11 | p.fd > 15))
      c.useQstaff(a.myqstaff)

  ?c.canUseBladeDS(smiteScr)
    c.useBladeDS()

  /****************************/
  ?c.canUseWeav()
    ?p.si = weavScr & p.fd < 15
      c.useWeav()
  /****************************/

  ?p.fd > 35
    p.pa = armor

    ?p.pa > 29 & p.php ! p.pmhp
      ?!keepMask
        equipL triskelion
        equipR ouroboros
      :
        equipL ouroboros

    :?14 <= p.pa & p.pa <= 29
      ldtF("default")
    :?p.pa < 14
      ldtF("armorRecover")
    :
      ldtF("extend")

  :?p.fd > 15
    ldtF("engage")

    ?hp ! maxhp & p.f = "pallas" & foe.state = 0
      ?!keepMask
        equipR ouroboros
      :
        equipL ouroboros
    :
      var isAbtToDash
      isAbtToDash = (p.fd <= 17)
      ?keepMask & isAbtToDash
        equipR @a.vshield@
      
      p.fds = foe.debuffs.string
      canDebI = (p.fds ! "chill" &
      ^f ! "immune_to_debuff_chill")
      p.bs = buffs.string

      ?!keepMask & canDebI & isAbtToDash &
      ^i.getFoeTotalHP() > i.getBuffInfo("smite", false, 2) * a.smiteMult
        equipR @a.aiShield@


  :?c.canBash()
    c.bash(a.dLsword)
  :?c.canDash()
    c.dash(a.dLsword,a.dshield)
  :
    p.fc = foe.count

    ?c.canUseHamm() & keepMask
    ^& !c.canUseBlade() &
    ^((smiteScr < 4 & p.si = 5) |
    ^ (smiteScr = 4 & p.si = 3))
      c.useHamm(a.myHamm)

    :?p.f = "spawner" & p.fc = 1
      fight1v1()

    :?p.f ! "pallas"
      p.fds = foe.debuffs.string
      canDebI = (p.fds ! "chill:6")
      canDebP = (p.fds ! "damage")
      canDebF = (p.fds ! "dot")
      
      ?canDebI | canDebP | canDebF
        ldtF("magic")
      :
        ldtF("wandDmg")

    : // p.f = "pallas"
      p.fs = foe.state

      ?p.fs = 100 & !pausedOnce
        // 100 = entering phase2
        pausedOnce = true
        ui.OpenInv()

      :?p.fs ! 4
        ?c.canUsePot()
          activate potion
        :?c.canUseMask()
          c.useMask()
        :
          fight1v1()



// *******************************************************
//                  END OF "5-Halls.txt"                  
// *******************************************************