// *******************************************************
//                   START OF "1-Rocky"                   
// *******************************************************

// Script for Rocky Plateau


// VARIABLES

var dontDash
var trueDmg

var nRuns = 0

var keepMask = true
?p.ll
  keepMask = false
  ?p.tt = 1
    nRuns++



// FUNCTIONS

func harvFunc(btn)
  btnPressed = true
  ?btn = yesBtn
    choice = true
  : // btn = noBtn
    choice = false
  
  return

func bossFight()

  ?p.f = "phase1"
    ?!keepMask & p.php = p.pmhp
      ?p.bs = "berserk"
        ldtF("fdmg")
      :
        c.doMoondial(Vantum Phoenix, a.fsword, 0)
    :
      equipL Vantum Phoenix

  :?p.f = "phase2"
    ?p.f = "poison"
      ?!keepMask & p.php = p.pmhp
        ?p.bs = "berserk"
          ldtF("idmg")
        :
          c.doMoondial(Bifrost, a.isword, 0)
      :
        equipL Bifrost
    :?p.f = "vigor"
      ?!keepMask & p.php = p.pmhp
        ?p.bs = "berserk"
          ldtF("pdmg")
        :
          c.doMoondial(Mamba Negra, a.psword, 0)
      :
        equipL Mamba Negra
    :?p.f = "aether"
      ?!keepMask & p.php = p.pmhp
        ?p.bs = "berserk"
          ldtF("vdmg")
        :
          c.doMoondial(Dothaneel, a.vsword, 0)
      :
        equipL Dothaneel
    :?p.f = "fire"
      ldtF("aeDmg")
    :?p.f = "ice"
      ?!keepMask & p.php = p.pmhp
        ?p.bs = "berserk"
          ldtF("fdmg")
        :
          c.doMoondial(Vantum Phoenix, a.fsword, 0)
      :
        equipL Vantum Phoenix

  : // ?foe = phase3
    ?c.canUsePotDmg()
      activate potion
    :?c.canUseMask()
      c.useMask()

    ?c.canUseCind()
      var cindMargin
      ?p.ls >= 13
        cindMargin = 1000
      :
        cindMargin = 3000
      ?p.fds = "ignition:15" |
      ^c.canKillCind(i.getFoeTotalHP(),
      ^              i.getDebuffLvl("ignition", true),
      ^              a.cindDmg, cindMargin)
        c.useCind()

    p.ft = foe.time

    ?((p.fs = 115 & p.ft < 70) |
    ^(p.fs = 32 & p.fdmg = 1))
    ^& p.ft >= 42 // putting up shield
      dontDash = true

      ?c.canUseMind()
        ldtF("escape")
      :?p.fd > 7
        equip @a.aebow@
      :?c.canUseMask()
        c.useMask()
    :
      dontDash = false
      ?p.fa > 0 & c.canUseHamm2() &
      ^!bardCdSlower & p.fd < 11
        c.useHamm()
      :
        ?p.fbs ! "defense_fire"
          ?!keepMask & p.php = p.pmhp
            ?p.bs = "berserk"
              ldtF("fdmg")
            :
              c.doMoondial(Vantum Phoenix, a.fsword, 0)
          :
            equipL Vantum Phoenix
        :
          ?!keepMask & p.php = p.pmhp
            ?p.bs = "berserk"
              ldtF("idmg")
            :
              c.doMoondial(Bifrost, a.isword, 0)
          :
            equipL Bifrost


  ?p.php < p.pmhp
  ^& !c.canBash() & !c.canDash()
  ^& !keepMask
    equipR @a.dLsword@


  return



// LOADOUTS

func ldtF(mode)
  ?mode = "default"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield1@
  :?mode = "armorRecover"
    equipL triskelion
    ?!keepMask
      equipR @a.cshield2@
  :?mode = "escape"
    equipL mind
    equipR @a.cshield2@

    //p.mindDelay = p.tt + p.mindCd
  :?mode = "magnet"
    equipL star
    ?!keepMask
      equipR triskelion


  :?mode = "harv"
    equip shovel


  //buffs/debuffs (oriented to duration)
  :?mode = "pDeb1"
    equipL @a.dPsword1@
    equipR mask
  :?mode = "pDeb2"
    equipL @a.dPsword1@ // put 2 when upgraded back to normal
    equipR mask
  :?mode = "aidActive"
    equipL @a.ahHammer@
    equipR @a.ahShield@
    
  :?mode = "fDeb"
    equipL @a.dFsword@
    equipR mask
  :?mode = "iDeb"
    equipL @a.dIsword@
    equipR mask
  //damage
  :?mode = "fDmg"
    equipL Vantum Phoenix
    ?!keepMask
      equipR @a.fsword@
  :?mode = "iDmg"
    equipL Bifrost
    ?!keepMask
      equipR @a.isword@
  :?mode = "pDmg"
    equipL Mamba Negra
    ?!keepMask
      equipR @a.psword@
  :?mode = "vDmg"
    equipL Dothaneel
    ?!keepMask
      equipR @a.vsword@
  :?mode = "aeDmg"
    equipL Void Slayer
    ?!keepMask
      equipR @a.aesword@


  : // print error message on screen
    i.showStrCentered(
    ^"ERROR: ldtF() recieved incorrect mode: " + mode,
    ^0, 30, "yellow")


  return 


// PROCEDURE

var choice = false
var btnPressed = false

var yesBtn
var noBtn

var dontShowAgain = false



?!btnPressed
  ?!yesBtn
    yesBtn = uimkr.mkBtn(
    ^-5,9,null,2,null,null, // x, y, w, h, anchor, dock,
    ^"Yes","#green", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound

  ?!noBtn
    noBtn = uimkr.mkBtn(
    ^5,9,8,2,null,null, // x, y, w, h, anchor, dock,
    ^"No","red", // txt, col,
    ^null,null,null, // tcol, bcol, hcol,
    ^-5,harvFunc, // style, pressed,
    ^null,null,null) // down, up, sound


  i.showStrCtr(6,30,
  ^"Harvest boulders?","#yellow",
  ^false)

:
  ?yesBtn
    yesBtn.Recycle()
    yesBtn = null
  ?noBtn
    noBtn.Recycle()
    noBtn = null


  ?!dontShowAgain
    var aux = p.tt + 3*u.sec
    ?p.tt < aux
      ?choice
        i.showStrCtr(6, 30,
        ^"Harvest boulders is enabled","#green",
        ^false)
      :
        i.showStrCtr(6, 30,
        ^"Harvest boulders is disabled","#red",
        ^false)
    :
      ?!dontShowAgain
        dontShowAgain = true



/* Mano izquierda:
--- hacer AAC ---
- cuando state = 3
--- NO hacer AAC ---
- Cuando tenemos la mascara puesta y el arma equipada es una espada D +21. */
?p.ils = 3 & !(keepMask & p.il = "sword D +21")
  equipL wand 0*
  equipL @p.il@
?p.irs = 3 & p.ir ! "sword D +21"
  equipR @a.cshield2@
  equipR @p.ir@


?debuffs.string | (p.tt > p.lavg & nRuns >= 2)
  loc.Leave()


?p.lb
  p.rs = res.stone
  p.rb = res.bronze

  ?p.rs >= 10 & p.rb >= 10
    ?p.ip ! "lucky"
      brew stone + bronze
  :
    loc.Leave()

  /*p.rw = res.wood
  p.rb = res.bronze
  ?p.rw >= 10 & p.rb >= 10
    ?p.ip ! "berserk"
      brew wood + bronze
  :
    loc.Leave()*/


?keepMask
  equipR mask


?p.lb | p.ll
  /* ldtF("default") but w/o considering keepMask*/
  equipL triskelion
  equipR @a.cshield1@


:?c.canUseFTali()
  equipL triskelion
  c.useFTali("r")
  
:?c.canUsePotDmg() & keepMask
  activate potion


:?choice & harvest.distance < 5
  ldtF("harv")

:?pickup.distance < 15
  ldtF("magnet")

  ?p.si < 3 & c.canUseQstaff()
    c.useQstaff(a.myqstaff)

:
  p.fd = foe.distance
  p.fs = foe.state
  p.fdmg = foe.damage
  p.php = hp
  p.pmhp = maxhp

  p.aiE = ai.enabled

  ?p.si < 3 & p.fd > 17 & c.canUseQstaff()
    c.useQstaff(a.myqstaff)

  :?p.f = "dysangelos" & p.fd <= 22
  ^& c.canUseBard2() &
  ^(p.fs = 100 | p.fs = 101 |
  ^p.fs = 102 | p.fs = 126)
    ?c.canUseHamm2()
      c.useHamm()
    :?c.canUseQstaff()
      c.useQstaff(a.myqstaff)
    :?c.canDash()
      c.dash(Vantum Phoenix, a.dshield)
    :
      p.bs = buffs.string
      ?p.bs = "berserk"
        c.useBard(Kubikiribocho)
      :
        c.useBard(a.fastBard)


  :?p.fd > 15 | !aiE |
  ^(p.fs = 124 | p.fs = -1 |
  ^(p.fs = 107 & p.fdmg = 3)) // Dysan's flying animation (after putting shield on phase3)
    p.fa = armor
    ?14 <= p.fa & p.fa <= 29
      ldtF("default")
    :
      ldtF("armorRecover")
      
  :?p.aiE & p.fs ! -1
    // fighting range

    p.f = foe
    p.fbs = foe.buffs.string
    p.fds = foe.debuffs.string
    p.fa = foe.armor

    p.bs = buffs.string

    ?p.f = "acronian_scout"
      ?c.canBash()
        c.bash(Vantum Phoenix)

      :
        ?keepMask
          ?p.fds ! "chill:6"
            equipL @a.dIsword@
          :?p.fds ! "damage"
            equipL @a.dPsword1@
          :?p.fds ! "dot"
            equipL @a.dFsword@
          :
            equipL Vantum Phoenix

        :
          ?p.php = p.pmhp
            c.doMoondial(Vantum Phoenix, a.fsword, 0)
          :
            equipL Vantum Phoenix
            equipR @a.dLsword@


    :?p.f = "dysangelos" &
    ^p.fs ! 124
    ^!(p.fs = 107 & p.fdmg = 3)

      p.fdmg = foe.damage
      p.ft = foe.time
      
      trueDmg = p.fdmg
      ?p.fds = "damage"
        ?p.f = "phase3"
          trueDmg -= 8
        :
          trueDmg -= 4

      ?p.fds = "feeble"
        trueDmg -= 2*i.getDebuffLvl("feeble", true)
      
      
      ?c.canBash() & !dontDash
        c.bash(Vantum Phoenix)
      :?c.canDash() & !dontDash
        c.dash(Vantum Phoenix, a.dshield)


      :?armor < trueDmg & p.fs = 32 & (
      ^(p.f = "phase1" &
      ^  ((p.fds = "chill" & p.ft > 58) // about to attack, slowed down by chill debuff
      ^ | (48 < p.ft & p.ft < 52)      )) | // same, but w/o chill (sometimes it runs out right when attacking)
      ^(p.f = "phase3" & p.ft > 80)  ) // final phase, about to normal attack
        ldtF("aidActive")


      :?p.f ! "immune_to_debuff_damage"
      ^& p.fbs ! "buff_protection"
      ^& p.ls > 5
        ?keepMask
          ?p.fds ! "chill:6"
            ldtF("iDeb")

          :?p.fds ! "damage"
            ?p.f = "phase3"
              ldtF("pDeb2")
            :
              ldtF("pDeb1")
          :
            ?p.fds ! "dot"
              ldtF("fDeb")
            :
              bossFight()
        :
          ?p.fds ! "damage"
            ?p.f = "phase3"
              ldtF("pDeb2")
            :
              ldtF("pDeb1")
          :
            bossFight()
      :
        bossFight()



// *******************************************************
//                    END OF "1-Rocky"                    
// *******************************************************