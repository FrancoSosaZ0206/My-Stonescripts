// PUBLIC VARIABLES

var DPSMeterEnabled = false


// GLOBAL VARIABLES

var dps_sec = 30  // 1 sec = 30 frames

var dps_tt
var currhp = 0
var nethp = 0
var dps_f

dps_tt  = totaltime
currhp = foe.hp + foe.armor
nethp  = foe.maxhp + foe.maxarmor
dps_f   = foe

var hits      = 0
var dmg       = 0
var totalDmg  = 0
var startTime = 0
var auxhp     = 0

var FRM = "N/A"
var FPH = "N/A"
var HPS = "N/A"
var DPF = "N/A"
var DPS = "N/A"



// PROCEDURE

// Calculate only when desired:
?DPSMeterEnabled

  ?dps_f = "boss" // only works when there's a boss

    ?0 < currhp & currhp < nethp // boss lost hp/armor, but isn't dead

      ?auxhp > currhp // if current hp updates
        hits += 1 // we count a hit
        dmg       = auxhp - currhp // we get the damage dealt from the difference
        totalDmg  = totalDmg + dmg // and we accumulate it in this variable
  
    : // else, reset temporal variables
      startTime = dps_tt
      hits      = 0
      dmg       = 0
      totalDmg  = 0


    FRM = dps_tt - startTime // total time minus the time the first hit was dealt
    ?hits > 1
      FPH = FRM      / (hits-1) // -1 is there to avoid stat flicking
    :
      FPH = "N/A"
  
    ?FPH ! "N/A"
      HPS = dps_sec      / FPH
    :
      HPS = "N/A"
      
    ?FRM > 0
      DPF = totalDmg / FRM // damage accumulated - no. frames elapsed
    :
      DPF = "N/A"

    ?DPF ! "N/A"
      DPS = DPF  * dps_sec
    :
      DPS = "N/A"



// PRIVATE FUNCTIONS (do not use/modify!)

func dpsUpdate()
  ?DPSMeterEnabled
    auxhp = currhp

  return


var hits_old
var HPS_old
var dmg_old
var DPF_old
var DPS_old



// PUBLIC FUNCTIONS

func dpsMeterUpdate(mode)
  var result = false

  ?DPSMeterEnabled
    ?dps_f = "boss"
      
      ?mode = 1
        ?(hits_old ! hits | HPS_old ! HPS |
        ^dmg_old ! dmg | DPF_old ! DPF |
        ^DPS_old ! DPS)
          ?hits_old ! hits
            hits_old = hits
          ?HPS_old ! HPS
            HPS_old = HPS
          ?dmg_old ! dmg
            dmg_old = dmg
          ?DPF_old ! DPF
            DPF_old = DPF
          ?DPS_old ! DPS
            DPS_old = DPS

          result = true


      :?mode = 2 &
      ^DPS_old ! DPS
        DPS_old = DPS

        result = true

  return result


func uiDPSMeter(x,y,colorStr,mode)
  var panel
  var txt
  
  ?DPSMeterEnabled & (mode = 1 | mode = 2)

    panel = ui.AddPanel()

    var dpsStr
    ?mode = 1
      dpsStr = "   DPS METER   "   + "\n" +
      ^        "···············"   + "\n" +
      ^        " hits     " + hits_old + "\n" +
      ^        " hits/s   " + HPS_old  + "\n" +
      ^        " dmg /hit " + dmg_old  + "\n" +
      ^        " dmg /f   " + DPF_old  + "\n" +
      ^        " dmg /s   " + DPS_old  + "\n" +
      ^        "···············"

    :?mode = 2
      dpsStr = "DPS | " + DPS_old

    txt = ui.AddText(dpsStr)


    panel.style = 3
    panel.Add(txt)
    
    ?mode = 1
      panel.anchor = top_left
      panel.dock = top_left
      txt.anchor = top_left
      txt.dock = top_left

    :?mode = 2
      panel.anchor = top_center
      panel.dock = top_center
      txt.anchor = top_left
      txt.dock = top_left
      
    
    panel.color = colorStr
    txt.color = colorStr


    panel.x = x
    panel.y = y
    
    txt.x = 1
    txt.y = 1
    
    ?mode = 1
      txt.w = 15
      txt.h = 8

    :?mode = 2
      txt.w = string.Size(dpsStr)
      txt.h = 1


    panel.w = txt.w + 2
    panel.h = txt.h + 2


    dpsUpdate()


  ?mode ! 1 & mode ! 2
    >c0,0,#red,func uiDPSMeter() - incorrect mode: @mode@

  return panel



