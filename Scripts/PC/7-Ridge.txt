// *******************************************************
//                       FILE INFO
// *******************************************************


// Script for Icy Ridge


//IMPORTS


var u = import MyScripts/Imports/utilities
var p
p = import MyScripts/Imports/performanceV2
var i
i = import MyScripts/Imports/MyBetterInfo
var c = import MyScripts/Imports/combat
var a = import MyScripts/Imports/MyArsenal




// VARIABLES




var smiteScr = 1
?smiteScr = 1
  ?loc.loop
    smiteScr = 2


var blowing


// LOADOUTS:


func ldtF(mode)


  ?mode = default
    equipL triskelion
    equipR @a.cshield@
  :?mode = engage
    equipL triskelion
    equipR @a.fshield@
  :?mode = escape
    equipL mind
    equipR @a.fshield@
  :?mode = magnet
    equipL star
    equipR triskelion


  :?mode = aidPassive
    equipL ouroboros
    equipR triskelion
  :?mode = aidActive
    equipL @a.fsword1@
    equipR @a.vsword1@


  :?mode = rangedDmg
    equip @a.fbow1@
  :?mode = rangedF
    equip @a.fbow2@


  :?mode = magicI
    equip @a.istaff@
  :?mode = magicP
    equip @a.pstaff@


  :?mode = meleeDmg
    equipL @a.fsword2@
    equipR @a.fsword3@
  :?mode = meleeDef1
    equipL @a.fsword1@
    equipR @a.fshield@
  :?mode = meleeDef2
    equipL @a.fsword2@
    equipR @a.fshield@


  :?mode = fDeb
    equipL @a.fsword2@
    equipR @a.fsword1@
  :?mode = iDeb
    equipL @a.fsword2@
    equipR @a.isword1@
  :?mode = pDeb
    equipL @a.fsword2@
    equipR @a.psword1@


  :?mode = AOEDeb
    equipL @a.fsword2@
    equipR @a.fsword1@


  :?mode = unmkDmg
    equipL @a.aeSWORD1@
    equipR @a.aeSWORD2@
  :?mode = unmkDef
    equipL @a.aeSWORD1@
    equipR @a.aeshield2@


  :
    >c-10,0,#red,
    ^ERROR: ldtF() recieved incorrect mode: @mode@
  return




// PROCEDURE:


var tui = i.showTimeUI(13,1,pink)
var cdui = i.showCdownUI(1,1,orange)


?p.tt % 30 = 0
  tui = i.showTimeUI(13,1,pink)
?i.cdUpdate()
  cdui = i.showCdownUI(1,1,orange)




?hp < maxhp
  loc.Leave()


?loc.begin
  ?p.ip ! berserk
    brew wood + bronze // berserk potion


?c.canUseTali()
  c.useTali(l)


:?pickup.distance ! 9999
  ?c.canUseQstaff()
    c.useQstaff(a.myqstaff)
  :
    ldtF(magnet)


:
  p.fd = foe.distance
  p.fs = foe.state


  ?p.f ! yeti
    p.si = screen.i
    ?c.canUseBladeDS(smiteScr)
      ?p.fd <= 33
        c.useBladeDS()


    ?c.canUseQstaff() & p.fd > 9
      c.useQstaff(a.myqstaff)
    :?p.fd > 35
      ldtF(default)
    :?p.fd > 15
      ldtF(engage)
    :?c.canBash()
      c.bash(a.isword2)
    :?c.canDash()
      c.dash(a.isword2,a.dshield)


    :
      p.fc = foe.count
      p.fn = foe.name


      ?p.f = spawner | p.fn = "Ice Wall"
        ?p.fd <= 6
          ldtF(unmkDmg)
        :
          ldtF(unmkDef)
      :
        ?p.fn = "Giant Ice Elemental" // the miniboss
          p.fds = foe.debuffs.string
          ?p.fds ! "damage"
            ldtF(pDeb)
          :
            ?p.fds ! "dot"
              ldtF(fDeb)
            :
              ?p.fhp > 500
                ldtF(rangedDmg)
              :
                ldtF(meleeDmg)
        :
          ?p.f = ice_elemental | p.f = ice_elemental_elite
            ?armor < 10
              ldtF(meleeDef1)
            :?p.fc > 1
              ldtF(magicI)
            :
              ldtF(meleeDmg)
          :
            ?c.canUseHamm() & (
            ^(smiteScr = 1 & p.si = 3) |
            ^(smiteScr = 2 & p.si = 1))
            ^& p.fd <= 5
              c.useHamm()


            :?p.fc = 1
              ?p.fds ! "chill:6"
                ldtF(iDeb)
              :
                ?p.fds ! "damage"
                  ldtF(pDeb)
                :
                  ?p.fds ! "dot"
                    ldtF(fDeb)
                  :
                    ldtF(meleeDmg)
            :
              p.fds = foe.debuffs.string
              ?p.fds ! "chill:6"
                ldtF(magicI)
              :
                ?p.fds ! "damage"
                  ldtF(magicP)
                :
                  ?p.fds ! "dot"
                    ldtF(meleeDef1)
                  :
                    ?p.fd <= 6
                      ldtF(unmkDmg)
                    :
                      ldtF(unmkDef)




  : // foe = yeti


    ?(p.fs = 133 | p.fs = 143)
    ^ & foe.time/u.sec <= 3
      blowing = true
    :
      blowing = false


    ?(c.canBash() | c.canDash())
    ^& (p.fhp < 500 | !blowing)
    ^& p.fs ! 1
      ?c.canBash()
        c.bash(a.fsword1)
      :?c.canDash()
        c.dash(a.fsword1,a.dshield)


    :?p.fs = 0 & p.fa > 0
      ?c.canUseHamm()
        c.useHamm()
      :?p.fa > 0
        equipL @a.althammer1@
        equipR @a.althammer2@


    :?p.fa <= 0 &
    ^(c.canUseMask() | c.canUseBard())
      ?c.canUseMask()
        c.useMask()
      :?c.canUseBard()
        c.useBard()


    :?p.fs = 1 | p.fs = 4
      ldtF(default)
    :
      p.fds = foe.debuffs.string


      ?c.canUseCind()
        ?fds = "ignition:10" |
        ^c.canKillCind(a.taliRDmg,500)
          c.useCind()


      ?!blowing
        ?c.canUsePotDmg()
          activate potion
        :
          ?c.canUseBard()
            c.useBard()
          :?p.fds ! "chill:6"
            ldtF(iDeb)
          :
            ?p.fds ! "dot"
              ldtF(fDeb)
            :
              ?c.canUseArm()
                ?buffs.string ! "pick_pocket:3"
                  equip arm
                :
                  c.useArm()
              :
                ldtF(meleeDmg)


      :?p.fd > 5 // blowing
        ?p.fds ! "chill:6"
          ldtF(magicI)
        :
          ?p.fds ! "dot"
            ldtF(rangedF)
          :
            ldtF(rangedDmg)


?c.canAAC(lh)
  c.useAAC(lh,a.altsword1)
?c.canAAC(rh)
  c.useAAC(rh,a.altshield)


// *******************************************************
//                      END OF FILE
// *******************************************************