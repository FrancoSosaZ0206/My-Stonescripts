// *******************************************************
//                START OF "Speed_O_Meter"                
// *******************************************************



/* FILE INFO
  Time Display Utility
  Made by MetalEagle26 (Tom Crow)

  current version: 1.9


DESCRIPTION
  This library provides functions to display your times on
    your runs!
  Includes some speedrunning versions to submit your times
    in frames ;)


HOW TO IMPORT
  PC:
    Put this file in C:/.../Stonescript/UI or your
    folder of preference
    and in your mindstone/ main script, do this:

    var som
    som = import UI/Speed_O_Meter

  Mobile:
    Copy-Paste this into your mindstone/script.


HOW TO USE
  Example for PC:
    ?som.Update()
      som.showSom(x,y,col)

  Example for Mobile (Recommended):
    var SOMPanel
    ?Update()
      SOMPanel = mkUI(x,y,col)

······················································

CHANGELOG:
  v1.2:
    - Now you can choose a slim version
      of the meter in all functions.
  v1.3:
    - showSpeedOmeterUI() renamed to "speedOmeterUI".
    - added import: UI_Maker.txt
    - speedOmeterUI now uses functions from
      the import "UI_Maker.txt".
  v1.4:
    - Fixed bug that prevented displaying
      the meter before the first
      second of each run.
  v1.5:
    - Now all functions that show the meter
      depend on speedOmeterToStr().
  v1.6:
    - formatSpeedrun(): now 1 digit frames
      are prefixed with a zero ('0').
    - myFormatDigital() and formatSpeedrun()
      moved to Utilities.
    - fixed ttSpeedrun update frequency. Now
      it updates every frame as it should.
  v1.7:
    - renamed all functions and variables
      containing "speedOmeter" to "som".
    - asciiPrint now belongs to Print() as
      one of two modes of a new parameter: isAscii.
    - uiSom() -> mkUI().
    - mkUI(): if isSlim, the panel will now be borderless.
    - ToString(): '#' replaced back with ' '.
      Now not all functions replace it again, but those who do
      delegate that task to Utilities -> replaceInStr().
  v1.8:
    - Renamed functions: removed "Som" from them
      to make them more concise and cohesive with
      object-oriented programming and the "new" keyword.
    - Added Import: Performance, to centralize dependencies
      and upgrade performance.
  v1.9:
    - Variable updates are now done in function Update().


······················································

Tip: keep in mind that speedrunning functions need to
  update constantly to show the frames, which may cause
  some lag.
  Also, remember to use ".Recycle()" function and assign
  to "null" after using UI functions!


Enjoy! */



// IMPORTS:

var u = new MyScripts/Lib/Utilities
var uimkr = new MyScripts/Lib/UI_Maker

var p
p = import MyScripts/Lib/Performance



// VARIABLES:

// Times in seconds
var ttDigital = u.myFormatDigital(p.tt)
var pb = u.myFormatDigital(p.lbt)
var avg = u.myFormatDigital(p.lavg)

// Times in seconds and frames
var ttSpeedrun = u.formatSpeedrun(p.tt)
var pbSpeedrun = u.formatSpeedrun(p.lbt)
var avgSpeedrun = u.formatSpeedrun(p.lavg)


// PUBLIC FUNCTIONS

func Update(isSpeedrun)
  // Variable updating frequency:
  ?p.tt % u.sec = 0
    ttDigital = u.myFormatDigital(p.tt)
  ttSpeedrun = u.formatSpeedrun(p.tt)

  ?p.ll
    pb = u.myFormatDigital(p.lbt)
    avg = u.myFormatDigital(p.lavg)

    pbSpeedrun = u.formatSpeedrun(p.lbt)
    avgSpeedrun = u.formatSpeedrun(p.lavg)


  ?isSpeedrun
    return true
  :
    return p.lb | p.ll | (p.tt % u.sec = 0)

func ToString(isSlim, isSpeedrun)

  ?isSlim
    ?isSpeedrun
      return
      ^"☆#" + pbSpeedrun +     // ☆ = PB
      ^"#|#≈#" + avgSpeedrun + // ≈ = AVG
      ^"#|#▶#" + ttSpeedrun    // ▶ = CURR

    :
      return
      ^"☆#" + pb +         // ☆ = PB
      ^"#|#≈#" + avg +      // ≈ = AVG
      ^"#|#▶#" + ttDigital // ▶ = CURR

  :
    ?isSpeedrun
      return
      ^"####SPEED-O-METER" + "\n" +
      ^"·····················" + "\n" +
      ^"#PB##" + pbSpeedrun + "#(" + p.lbt + "f)" + "\n" +
      ^"#AVG#" + avgSpeedrun + "#(" + p.lavg + "f)" + "\n" +
      ^"#CUR#" + ttSpeedrun + "#(" + p.tt + "f)"

    :
      return
      ^"SPEED-O-METER" + "\n" +
      ^"·············" + "\n" +
      ^"##PB##" + pb + "\n" +
      ^"##AVG#" + avg + "\n" +
      ^"##CUR#" + ttDigital

/* isAscii:
    true: prints the text in ascii format,
          replacing the whitespace characters
          with transparent characters.
    false: prints normally. Whitespaces
           will block the background. */
func Print(x, y, col, isSlim, isSpeedrun, isAscii)
  ?asciiPrint

    >`@x@,@y@,@col@,ascii
@ToString(isSlim, isSpeedrun)@
asciiend

  :
    >`@x@,@y@,@col@,@ToString(isSlim, isSpeedrun)@

  return

func mkUI(x, y, col, isSlim, isSpeedrun)

  var txt
  var panel

  // For the panel:
  var pnlAnchor
  var pnlDock
  var style
  
  ?isSlim
    pnlAnchor = top_center
    pnlDock = top_center
    style = -1
  :
    pnlAnchor = top_left
    pnlDock = top_left
    style = -2

  txt = uimkr.mkTxtbox(
  ^1, 1, null, null, // x, y, w, h,
  ^top_left, top_left, // pnlAnchor, dock,
  ^ToString(isSlim, isSpeedrun), // txt
  ^null, col) // align, col

  panel = uimkr.mkPanel(
    ^x, y, txt.w + 2, txt.h + 2, // x, y, w, h,
    ^pnlAnchor, pnlDock, // anchor, dock,
    ^col, style, txt) // col, style, child

  return panel




// *******************************************************
//                 END OF "Speed_O_Meter"                 
// *******************************************************
